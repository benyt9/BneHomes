plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'bne.bneplugins.bnehomes'
version = '1.5-BETA'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "Velocity"
        url = "https://nexus.velocitypowered.com/repository/maven-public/"
    }
}

dependencies {
    // Paper/Bukkit API
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")

    // Velocity API
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'

    // SQLite
    implementation 'org.xerial:sqlite-jdbc:3.41.2.1'
}

java {
    def targetJavaVersion = 21
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isJava10Compatible()) {
        options.release.set(21)
    }
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

// Ressourcen verarbeiten (Version in plugin.yml einsetzen)
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// Duplikate verhindern (velocity-plugin.json)
tasks.jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
